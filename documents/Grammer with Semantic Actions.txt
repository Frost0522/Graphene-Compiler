      DEFINITION-LIST ::= DEFINITION MAKE-DEF-EXP DEFINITION-LIST MAKE-DEF-LST-EXP
                        | ε

           DEFINITION ::= "fn" IDENTIFIER "(" PARAMETER-LIST ")" "->" TYPE
                              BODY MAKE-BODY-EXP

       PARAMETER-LIST ::= FORMAL-PARAMETERS
                        | "ε"

    FORMAL-PARAMETERS ::= ID-WITH-TYPE MAKE-IWT-EXP ID-WITH-TYPE'    

	   ID-WITH-TYPE' ::= "," FORMAL-PARAMETERS MAKE-FRML-PARAM-EXP
					| ε

         ID-WITH-TYPE ::= IDENTIFIER ":" TYPE

                 TYPE ::= "integer" MAKE-INT-EXP
                        | "boolean" MAKE-BOOL-EXP

                 BODY ::= PRINT-EXPRESSION BODY
                        | EXPRESSION

     PRINT-EXPRESSION ::= "print" MAKE-PRINT-EXP "(" EXPRESSION ")"

           EXPRESSION ::= SIMPLE-EXPRESSION SIMPLE-EXPRESSION'

   SIMPLE-EXPRESSION' ::= "==" MAKE-EQUALS-EXP EXPRESSION
					| "<" MAKE-LESS-THAN-EXP EXPRESSION
					| ε


    SIMPLE-EXPRESSION ::= TERM TERM'

	           TERM' ::= "or" TERM MAKE-OR-EXP TERM'
					| "+" TERM MAKE-PLUS-EXP TERM'
					| "-" TERM MAKE-MINUS-EXP TERM'
					| ε

                 TERM ::= FACTOR FACTOR'

              FACTOR' ::= "and" FACTOR MAKE-AND-EXP FACTOR'
                        | "*" FACTOR MAKE-TIMES-EXP FACTOR'
                        | "/" FACTOR MAKE-DIVIDE-EXP FACTOR'
					| ε

               FACTOR ::= LITERAL
                        | "not" MAKE-NOT-EXP FACTOR
                        | "-" MAKE-NEG-EXP FACTOR
                        | IDENTIFIER MAKE-ID-EXP IDENTIFIER'
                        | "if" MAKE-IF-EXP "(" EXPRESSION ")" MAKE-THEN-EXP EXPRESSION "else" MAKE-ELSE-EXP EXPRESSION
                        | "(" EXPRESSION ")"

             IDENTIFIER' ::= "(" ARGUMENT-LIST ")"
                        | ε
	
        ARGUMENT-LIST ::= FORMAL-ARGUMENTS MAKE-FUNCT-EXP
                        | ε

     FORMAL-ARGUMENTS ::= EXPRESSION EXPRESSION'

          EXPRESSION' ::= "," FORMAL-ARGUMENTS MAKE-FRML-ARGS-EXP
                        | ε

              LITERAL ::= BOOLEAN-LITERAL MAKE-BOOL-LIT-EXP
                        | INTEGER-LITERAL MAKE-INT-LIT-EXP